//===== Library =====
#include <WiFi.h>
#include <HTTPClient.h>
#include <ModbusMaster.h>

//===== Define Pins =====
#define MAX485_RE_DE 23
#define RXD2         16
#define TXD2         17

//===== Modbus Config =====
#define SLAVE_ID     0x0C
ModbusMaster node;

//===== WiFi + Google Apps Script =====
const char* ssid = "TrueGigatexFiber_2.4G_CYD";
const char* password = "KhongYim";
const char* scriptURL = "https://script.google.com/macros/s/AKfycbyk-yE-SKbFYiVltWkbTjicBgEnaMIEGmYjyJPyo_RTzfRcJW2007y-Bi55mkVLtnA/exec"; 
//===== MAX485 Control =====
void preTransmission() {
  digitalWrite(MAX485_RE_DE, HIGH);
  delay(2);
}

void postTransmission() {
  delay(2);
  digitalWrite(MAX485_RE_DE, LOW);
}

//===== ‡∏ü‡∏±‡∏á‡∏Å‡πå‡∏ä‡∏±‡∏ô‡∏™‡πà‡∏á‡∏Ç‡πâ‡∏≠‡∏°‡∏π‡∏•‡πÑ‡∏õ Google Sheet =====
void SendData(float waterlevel) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    http.begin(scriptURL);
    http.addHeader("Content-Type", "application/json");

    String id = "esp001";  // ‡∏´‡∏£‡∏∑‡∏≠‡πÉ‡∏ä‡πâ getChipID() ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡πÉ‡∏´‡πâ‡πÄ‡∏õ‡πá‡∏ô unique
    String jsonData = "{\"id\":\"" + id + "\",\"waterlevel\":" + String(waterlevel, 1) + "}";
    // String jsonData = "{\"waterlevel\":" + String(waterlevel, 1) + "}";
    Serial.println("JSON Data: " + jsonData);

    int httpResponseCode = http.POST(jsonData);

    if (httpResponseCode > 0) {
      String response = http.getString();
      Serial.println("Server Response: " + response);
    } else {
      Serial.print("Error code: ");
      Serial.println(httpResponseCode);
      // Serial.println(http.errorToString(httpResponseCode));
    }

    http.end();
  } else {
    Serial.println("WiFi Disconnected");
  }
}

//===== ‡∏ü‡∏±‡∏á‡∏Å‡πå‡∏ä‡∏±‡∏ô Scan WiFi (Debug) =====
void scanWiFi() {
  Serial.println("üîç Scanning for WiFi networks...");
  int n = WiFi.scanNetworks();
  if (n == 0) {
    Serial.println("‚ùå No networks found.");
  } else {
    Serial.printf("‚úÖ Found %d networks:\n", n);
    for (int i = 0; i < n; ++i) {
      Serial.printf("üîπ SSID: %s | Signal: %d dBm | %s\n",
                    WiFi.SSID(i).c_str(), WiFi.RSSI(i),
                    (WiFi.encryptionType(i) == WIFI_AUTH_OPEN) ? "Open" : "Secured");
      delay(10);
    }
  }
}

//===== Setup =====
void setup() {
  Serial.begin(115200);
  delay(1000);
  scanWiFi();

  Serial.print("üîå Connecting to WiFi: ");
  Serial.println(ssid);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }

  Serial.println("\n‚úÖ WiFi connected!");

  // RS485 & Modbus Init
  Serial2.begin(115200, SERIAL_8N1, RXD2, TXD2);
  pinMode(MAX485_RE_DE, OUTPUT);
  digitalWrite(MAX485_RE_DE, LOW);

  node.begin(SLAVE_ID, Serial2);
  node.preTransmission(preTransmission);
  node.postTransmission(postTransmission);

  // ‡∏≠‡πà‡∏≤‡∏ô PID/VID
  uint8_t result = node.readHoldingRegisters(0x00, 2);
  if (result == node.ku8MBSuccess) {
    //Serial.print(F("PID: 0x")); Serial.println(node.getResponseBuffer(0), HEX);
    //Serial.print(F("VID: 0x")); Serial.println(node.getResponseBuffer(1), HEX);
  } else {
    Serial.print(F("PID/VID read failed, code=")); Serial.println(result);
  }

  // ‡∏ï‡∏±‡πâ‡∏á‡∏Ñ‡πà‡∏≤ Trigger Mode
  uint16_t ctrl = (1 << 2); // Trigger mode only
  ctrl &= ~(1 << 0); // ‡∏õ‡∏¥‡∏î internal waterlevel
  ctrl &= ~(1 << 1); // ‡∏õ‡∏¥‡∏î waterlevel compensation
  result = node.writeSingleRegister(0x08, ctrl);
  if (result == node.ku8MBSuccess) {
    Serial.println(F("Control register set OK"));
  } else {
    Serial.print(F("Control register write failed, code=")); Serial.println(result);
  }
}

//===== Loop =====
void loop() {
  static unsigned long lastRun = 0;

  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("WiFi disconnected, reconnecting...");
    WiFi.reconnect();
    delay(1000);
    return;
  }

  // ‡∏≠‡πà‡∏≤‡∏ô‡∏Ñ‡πà‡∏≤‡∏ó‡∏∏‡∏Å 5 ‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ
  if (millis() - lastRun >= 10000) {
    lastRun = millis();

    // Trigger ‡∏ß‡∏±‡∏î‡∏£‡∏∞‡∏¢‡∏∞
    uint16_t ctrl = (1 << 2) | (1 << 3);
    node.writeSingleRegister(0x08, ctrl);
    delay(1000);

    uint8_t result = node.readHoldingRegisters(0x05, 1);
    if (result == node.ku8MBSuccess) {
      float distance_cm = node.getResponseBuffer(0) / 100.0;
      float waterlevel = 100.0 - distance_cm;
      waterlevel = round(waterlevel * 10.0) / 10.0;
      Serial.print(F("üìè Water Level: ")); Serial.print(waterlevel, 1); Serial.println(F(" cm"));
      SendData(waterlevel); // ‡∏™‡πà‡∏á‡∏Ñ‡πà‡∏≤‡∏£‡∏∞‡∏î‡∏±‡∏ö‡∏ô‡πâ‡∏≥‡∏Ç‡∏∂‡πâ‡∏ô Google Sheet
    } else {
      Serial.print(F("‚ùå Read failed, code=")); Serial.println(result);
    }
  }
}
